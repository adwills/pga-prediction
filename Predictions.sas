LIBNAME GOLF "C:\Users\anddwi\Andrew\Projects\Golf\Data";

PROC SORT DATA=GOLF.PGA_PREDICTIONS_2STAGE OUT=CUTRANK(KEEP=EM_EVENTPROBABILITY);
	BY DESCENDING EM_EVENTPROBABILITY;
RUN;
/***********************Temporary Change to Cut Line for WGC**************************/
DATA _NULL_;
	SET CUTRANK;
	BY DESCENDING EM_EVENTPROBABILITY;
	IF _N_=70 THEN DO;
		CALL SYMPUTX('CUTLINE',EM_EVENTPROBABILITY);
	END;
RUN;

%PUT CUTLINE=&CUTLINE;

DATA PGA_PREDICTIONS_2STAGE_INT;
	SET GOLF.PGA_PREDICTIONS_2STAGE;
	IF EM_EVENTPROBABILITY >= &CUTLINE THEN MAKE_CUT_PROB=1;
	ELSE MAKE_CUT_PROB =0;
	IF _N_ <= 70 THEN MAKE_CUT_FINISH=1;
	ELSE MAKE_CUT_FINISH =0;
	EM_CLASS=INPUT(EM_CLASSIFICATION,1.);
	P_FINISH_INT=_N_;
RUN;

PROC FREQ DATA=PGA_PREDICTIONS_2STAGE_INT;
	TABLES MAKE_CUT_PROB*MAKE_CUT_FINISH / LIST;
RUN;

PROC PRINT DATA=PGA_PREDICTIONS_2STAGE_INT;
	VAR NAMEF NAMEL PID P_FINISH P_FINISH_INT MAKE_CUT_PROB EM_EVENTPROBABILITY MAKE_CUT_FINISH;
RUN;

/* RUN AFTER TOURNAMENT FINISHES */

PROC SQL;
	CREATE TABLE ERRORS AS 
	SELECT A.PID, A.NAMEF, A.NAMEL, A.TOURNID, A.YEAR, A.P_FINISH, A.P_FINISH_INT, A.EM_CLASSIFICATION, A.MAKE_CUT_PROB,
		B.FINISH, B.MADE_CUT,
		(B.FINISH-A.P_FINISH)**2 AS SQUARED_ERROR, (B.FINISH-A.P_FINISH_INT)**2 AS SQUARED_ERROR_INT
	FROM PGA_PREDICTIONS_2STAGE_INT A
		INNER JOIN GOLF.PGA_TRAINING B
		ON A.PID=B.PID
		AND A.TOURNID=B.TOURNID
		AND A.YEAR=B.YEAR;
QUIT;

PROC SQL;
	CREATE TABLE PERF AS
	SELECT TODAY() AS DATE FORMAT=DATE9., A.YEAR, A.TOURNID, A.NUM, B.DENOM, A.NUM/B.DENOM AS MISC_RATE, C.ASE, C.ASE_INT, C.SQRT_ASE_INT
	FROM (SELECT DISTINCT YEAR, TOURNID, COUNT(*) AS NUM FROM ERRORS WHERE MAKE_CUT_PROB NE MADE_CUT AND ^MISSING(MADE_CUT)) A, 
	(SELECT COUNT(*) AS DENOM FROM ERRORS WHERE ^MISSING(MADE_CUT)) B,
	(SELECT SUM(SQUARED_ERROR)/COUNT(*) AS ASE, SUM(SQUARED_ERROR_INT)/COUNT(*) AS ASE_INT, SQRT(SUM(SQUARED_ERROR_INT)/COUNT(*)) AS SQRT_ASE_INT
	FROM ERRORS WHERE ^MISSING(FINISH)) C;
QUIT;

PROC APPEND BASE=GOLF.MODEL_PERFORMANCE DATA=PERF;
RUN;

PROC SORT DATA=GOLF.MODEL_PERFORMANCE NODUP;
	BY DATE YEAR TOURNID;
RUN;

proc sgplot data=GOLF.MODEL_PERFORMANCE;
	TITLE "Misclassifcation (Cut) and ASE (Finish) over Time";
	series x=DATE y=MISC_RATE /;
	series x=DATE y=SQRT_ASE_INT / Y2AXIS;
	xaxis grid;
	yaxis grid;
run;

TITLE;

PROC FREQ DATA=ERRORS;
	TABLES EM_CLASSIFICATION*MADE_CUT / LIST ;
RUN;

PROC FREQ DATA=ERRORS;
	TABLES MAKE_CUT_PROB*MADE_CUT / LIST ;
RUN;
